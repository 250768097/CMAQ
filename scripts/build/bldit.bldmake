#!/bin/csh -f

# ==================== BLDMAKEv5.1 Build Script ===================== 
# Usage: bldit.bldmake >&! bldit.bldmake.log                          
#
# To report problems or request help with this script/program:        
#             http://www.cmascenter.org
# =================================================================== 

#> Source the config.cmaq file to set the build environment
 source ../config.cmaq

#> Check for M3HOME and M3LIB settings:
 if ( ! -e $M3HOME || ! -e $M3LIB ) then
    echo "   $M3HOME or $M3LIB directory not found"
    exit 1
 endif
 echo "    Model repository base path: $M3HOME"
 echo "                  library path: $M3LIB"

#> If $M3MODEL not set, default to $M3HOME
 if ( $?M3MODEL ) then
    echo "         Model repository path: $M3MODEL"
 else
    setenv M3MODEL $M3HOME
    echo " default Model repository path: $M3MODEL"
 endif

 set BLD_OS = `uname -s`        ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host!"
    exit 1
 endif

 set echo

#:#:#:#:#:#:#:#:#:#:#:# Begin User Input Section #:#:#:#:#:#:#:#:#:#:#:#

#> Source Code Repository
 setenv REPOROOT $M3MODEL/BLDMAKE      #> location of the BldMake source code

#> Working directory and application IDs
 set Base = $cwd                       #> working directory for compiling the source code
 set EXEC = bldmake                    #> executable name

# set fortran compiler and flags
 set FC = ${myFC}                      #> path of Fortan compiler; set in config.cmaq
 set FC_FLAGS = "-c ${myFFLAGS}"       #> Fortran compiler flags; set in config.cmaq

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#

 set DEST = $M3LIB
 set Bld = $Base/BLD
 if ( ! -e "$Bld" ) then
    mkdir $Bld
    else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
       else
       echo "Deleting files in $Bld"
       /bin/rm -f $Bld/*
       endif
    endif
 cd $Bld

 set flist = (\
      ${REPOROOT}/cfg_module.f\
      ${REPOROOT}/bldmake.f\
      ${REPOROOT}/parser.f\
      ${REPOROOT}/utils.f )

#> compile flist
 foreach file ( $flist )
    $FC $FC_FLAGS $file
    end

 $FC *.o -o $EXEC
 if( ! -e $EXEC ) then
  echo " "; echo " ***ERROR*** Compile failed"; echo " "
  exit 1
  endif

 if ( ! -d "$DEST" ) mkdir -p $DEST
 cd $DEST
 if ( -e $EXEC ) then
    mv -f $EXEC $EXEC.old
    endif

 mv ${Bld}/${EXEC} $EXEC
 chmod 755 $EXEC

 echo " "; echo " Finish building $EXEC "

 exit()
