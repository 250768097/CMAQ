#!/bin/csh -f

# ======================== CCTMv5.1 Build Script ======================= 
# Usage: bldit.cctm >&! bldit.cctm.log                                   
# Requirements: I/O API & netCDF libraries, a Fortran compiler,               
#               and MPI for multiprocessor computing                     
#
# To report problems or request help with this script/program:           
#             http://www.cmascenter.org
# ====================================================================== 

#> Source the config.cmaq file to set the build environment
 source ../config.cmaq

 set echo

#:#:#:#:#:#:#:#:#:#:#:# Begin User Input Section #:#:#:#:#:#:#:#:#:#:#:#

#> Source Code Repository
 setenv REPOROOT $M3MODEL/CCTM         #> location of the CCTM source code
 set GlobInc = $REPOROOT/ICL           #> location of the global include files
 set Mechs   = $REPOROOT/MECHS         #> location of the chemistry mechanism include files

#> Working directory and application IDs
 set Base = $cwd                       #> working directory for compiling the source code
 set APPL  = D51a                      #> model configuration ID
 set MODEL = CCTM_${APPL}_$EXEC_ID     #> executable name
 set CFG   = cfg.$MODEL                #> configuration file name

#> Controls for managing the source code and MPI compilation
 set CopySrc                           #> copy the source files into a working directory
#set MakeFileOnly                      #> uncomment to build a Makefile, but do not compile; comment out to compile the model (default if not set)
set ParOpt                             #> uncomment to build a multiple processor (MPI) executable; comment out for a single processor (serial) executable
#set build_parallel_io                 #> uncomment to build with parallel I/O (pnetcdf); comment out to use standard netCDF I/O

#> Two-way WRF-CMAQ 
#set build_twoway                      #> uncomment to build WRF-CMAQ twoway; comment out for off-line chemistry 

#======================================#>
#> CCTM Science Modules
#======================================#>
#> NOTE: For the modules with multiple options, a note is provided on where to look in the CCTM
#>   source code archive for a list of the possible settings; users may also refer to the CMAQ documentation

 set ModDriver = driver/wrf            #> generalized coordinate driver module (see $M3MODEL/CCTM/driver)
 set ModInit   = init/yamo             #> time-step initialization module 
 set ModGrid   = grid/cartesian        #> grid configuration module 
 set ModCpl    = couple/gencoor_wrf    #> unit conversion and concentration coupling module (see $M3MODEL/CCTM/couple)
 set ModHadv   = hadv/yamo             #> horizontal advection module
 set ModVadv   = vadv/wrf              #> vertical advection module (see $M3MODEL/CCTM/vadv)
 set ModHdiff  = hdiff/multiscale      #> horizontal diffusion module
 set ModVdiff  = vdiff/acm2            #> vertical diffusion module (see $M3MODEL/CCTM/vdiff)
 set ModDepv   = depv/m3dry            #> deposition velocity calculation module (see $M3MODEL/CCTM/depv)
 set ModEmis   = emis/emis             #> in-line emissions module
 set ModBiog   = biog/beis3            #> BEIS3 in-line emissions module 
 set ModPlmrs  = plrise/smoke          #> in-line emissions plume rise
 set ModCgrds  = spcs/cgrid_spcs_nml   #> chemistry species configuration module (see $M3MODEL/CCTM/spcs)
 set ModPhot   = phot/inline           #> photolysis calculation module (see $M3MODEL/CCTM/phot)
 set Mechanism = cb05e51_ae6_aq        #> chemical mechanism (see $M3MODEL/CCTM/MECHS)
 set ModGas    = gas/ebi_${Mechanism}  #> gas-phase chemistry solver (see $M3MODEL/CCTM/gas)
 set ModAero   = aero/aero6            #> aerosol chemistry module (see $M3MODEL/CCTM/aero)
 set ModCloud  = cloud/acm_ae6         #> cloud chemistry module (see $M3MODEL/CCTM/cloud)
 set ModUtil   = util/util             #> CCTM utility modules
 set Tracer    = trac0                 #> tracer configuration directory under $M3MODEL/CCTM/MECHS [ default: no tracer species ]
 set PAOpt     = pa_noop               #> name of process analysis include file directory under $M3MODEL/CCTM/ICL

#======================================#>
#> Computing System Configuration:
#>    Most of these settings are done in config.cmaq
#======================================#>

 set FC = ${myFC}                      #> path of Fortan compiler; set in config.cmaq
 set FP = $FC                          #> path of Fortan preprocessor; set in config.cmaq
 set CC = ${myCC}                      #> path of C compiler; set in config.cmaq
 set Blder = $M3LIB/bldmake            #> location of model builder executable

#> Libraries/include files
 set LIOAPI   = "${IOAPI_DIR}/lib ${ioapi_lib}"      #> I/O API library directory
 set IOAPIMOD = "${IOAPI_DIR}/include"               #> I/O API module directory
 set NETCDF   = "${NETCDF_DIR}/lib ${netcdf_lib}"    #> netCDF library directory
 set PNETCDF  = "${PNETCDF_DIR}/lib ${pnetcdf_lib}"  #> Parallel netCDF library directory
 set PIO_INC = "${IOAPI_DIR}/src"

#> Compiler flags set in config.cmaq
 set F_FLAGS    = "${myFFLAGS} -I ${IOAPIMOD} -I ${MPI_INC}  -I ${PIO_INC} -I." #> F77 flags
 set F90_FLAGS  = "${myFRFLAGS} -I ${IOAPIMOD} -I ${MPI_INC} -I ${PIO_INC} -I." #> F90 flags
 set CPP_FLAGS  = "" #> Fortran preprocessor flags
 set C_FLAGS    = "${myCFLAGS} -DFLDMN -I ${MPI_INC}" #> C flags
 set LINK_FLAGS = "${myLINK_FLAG}" # Link flags

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

#> Check for M3HOME and M3LIB settings:
 if ( ! -e $M3HOME || ! -e $M3LIB ) then
    echo "   $M3HOME or $M3LIB directory not found"
    exit 1
 endif
 echo "    Model repository base path: $M3HOME"
 echo "                  library path: $M3LIB"

#> If $M3MODEL not set, default to $M3HOME
 if ( $?M3MODEL ) then
    echo "         Model repository path: $M3MODEL"
 else
    setenv M3MODEL $M3HOME
    echo " default Model repository path: $M3MODEL"
 endif

 set BLD_OS = `uname -s`        ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host!"
    exit 1
 endif
 
if ( $?build_twoway ) then
    set MakeFileOnly   # do not compile coupled model, only generate makefile
    set ModTwoway = twoway
 endif

 if ( $?build_parallel_io ) then
    set PIO = ( -Dparallel_io )
 else
    set PIO = ""
 endif

 if ( $?ParOpt ) then      # Multiprocessor system configuration
    echo "   Parallel; set MPI flags"
    set ModStenex = STENEX/se
    set ModPario = PARIO
    set ModPar = par/mpi
    set PARIO = ${M3MODEL}/PARIO
    set STENEX = ${M3MODEL}/STENEX
    # MPI_INC is set in config.cmaq
 #   set PIO_INC = "${IOAPI_DIR}/src/fixed_src"
    set PAR = ( -Dparallel )
    set Popt = SE
    set seL = se_snl
    set LIB3 = "-L${M3LIB}/mpi ${mpi} ${extra_lib}"
    set Str1 = (// Parallel / Include message passing definitions)
    set Str2 = (include SUBST_MPI mpif.h;)
 else
    echo "   Not Parallel; set Serial (no-op) flags"
    set ModStenex = STENEX/noop
    set ModPar = par/par_noop
    set PARIO = "."
    set STENEX = ${M3MODEL}/STENEX/noop
    set MPI_INC = "."
 #   set PIO_INC = "."
    set PAR = ""
    set Popt = NOOP
    set seL = sef90_noop
    set LIB3 = "${extra_lib}"
    set Str1 =
    set Str2 =
 endif 
 
#> Process Analysis code and module locations
 set ModPa     = procan/pa             #> process analysis module location
 set PABase    = $GlobInc              #> location of process analysis include files

#> Mechanism location
 set ModMech = MECHS/$Mechanism        #> chemical mechanism module

#> Set and create the "BLD" directory for checking out and compiling source code
 set Bld = $Base/BLD_${APPL}
 if ( ! -e "$Bld" ) then
    mkdir $Bld
 else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
    endif
 endif
 cd $Bld

 set LIB1 = "-L${LIOAPI}"
 if ( $?build_parallel_io ) then
    set LIB2 = "-L${NETCDF} -L${PNETCDF}"
 else
    set LIB2 = "-L${NETCDF}"
 endif
 set LIBS = "$LIB1 $LIB2 $LIB3"
 
 set ICL_PAR   = $GlobInc/fixed/mpi
 set ICL_CONST = $GlobInc/fixed/const
 set ICL_FILES = $GlobInc/fixed/filenames
 set ICL_EMCTL = $GlobInc/fixed/emctrl
 set ICL_PA    = $GlobInc/procan/$PAOpt

 if ( $?CopySrc ) then
    /bin/cp -fp ${ICL_PAR}/*   ${Bld}
    /bin/cp -fp ${ICL_CONST}/* ${Bld}
    /bin/cp -fp ${ICL_FILES}/* ${Bld}
    /bin/cp -fp ${ICL_EMCTL}/* ${Bld}
    /bin/cp -fp ${ICL_PA}/*    ${Bld}
 endif

 set ICL_PAR   = .
 set ICL_CONST = .
 set ICL_FILES = .
 set ICL_EMCTL = .
 set ICL_PA    = .

 set STX1 = ( -DSUBST_BARRIER=${Popt}_BARRIER\
              -DSUBST_GLOBAL_MAX=${Popt}_GLOBAL_MAX\
              -DSUBST_GLOBAL_MIN=${Popt}_GLOBAL_MIN\
              -DSUBST_GLOBAL_MIN_DATA=${Popt}_GLOBAL_MIN_DATA\
              -DSUBST_GLOBAL_TO_LOCAL_COORD=${Popt}_GLOBAL_TO_LOCAL_COORD\
              -DSUBST_GLOBAL_SUM=${Popt}_GLOBAL_SUM\
              -DSUBST_GLOBAL_LOGICAL=${Popt}_GLOBAL_LOGICAL\
              -DSUBST_LOOP_INDEX=${Popt}_LOOP_INDEX\
              -DSUBST_SUBGRID_INDEX=${Popt}_SUBGRID_INDEX )
 set STX2 = ( -DSUBST_HI_LO_BND_PE=${Popt}_HI_LO_BND_PE\
              -DSUBST_SUM_CHK=${Popt}_SUM_CHK\
              -DSUBST_INIT_ARRAY=${Popt}_INIT_ARRAY\
              -DSUBST_COMM=${Popt}_COMM\
              -DSUBST_MY_REGION=${Popt}_MY_REGION\
              -DSUBST_SLICE=${Popt}_SLICE\
              -DSUBST_GATHER=${Popt}_GATHER\
              -DSUBST_DATA_COPY=${Popt}_DATA_COPY\
              -DSUBST_IN_SYN=${Popt}_IN_SYN )

#> make the config file

 set Cfile = ${CFG}.bld
 set quote = '"'

 echo                                                               > $Cfile
 echo "model       $MODEL;"                                        >> $Cfile
 echo                                                              >> $Cfile
 echo "FPP         $FP;"                                           >> $Cfile
 echo                                                              >> $Cfile
 set text = "$quote$CPP_FLAGS $PAR $PIO $STX1 $STX2$quote;"
 echo "cpp_flags   $text"                                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f_compiler  $FC;"                                           >> $Cfile
 echo                                                              >> $Cfile
 echo "f_flags     $quote$F_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f90_flags   $quote$F90_FLAGS$quote;"                        >> $Cfile
 echo                                                              >> $Cfile
 echo "c_compiler  $CC;"                                           >> $Cfile
 echo                                                              >> $Cfile
 echo "c_flags     $quote$C_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "link_flags  $quote$LINK_FLAGS$quote;"                       >> $Cfile
 echo                                                              >> $Cfile
 echo "libraries   $quote$LIBS$quote;"                             >> $Cfile
 echo                                                              >> $Cfile

 set text="// mechanism:"
 echo "$text ${Mechanism}"                                         >> $Cfile
 echo "// model repository: ${M3MODEL}"                            >> $Cfile
 echo                                                              >> $Cfile
 echo "include SUBST_PE_COMM    $ICL_PAR/PE_COMM.EXT;"             >> $Cfile
 echo "include SUBST_CONST      $ICL_CONST/CONST.EXT;"             >> $Cfile
 echo "include SUBST_FILES_ID   $ICL_FILES/FILES_CTM.EXT;"         >> $Cfile
 echo "include SUBST_EMISPRM    $ICL_EMCTL/EMISPRM.EXT;"           >> $Cfile
 echo                                                              >> $Cfile

 set text = "// Process Analysis / Integrated Reaction Rates processing"
 echo $text                                                        >> $Cfile
 echo "include SUBST_PACTL_ID    $ICL_PA/PA_CTL.EXT;"              >> $Cfile
 echo "include SUBST_PACMN_ID    $ICL_PA/PA_CMN.EXT;"              >> $Cfile
 echo "include SUBST_PADAT_ID    $ICL_PA/PA_DAT.EXT;"              >> $Cfile
 echo                                                              >> $Cfile

 echo "$Str1"                                                      >> $Cfile
 if ( $compiler == gfort ) then
    echo "include SUBST_MPI ${MPI_INC}/mpif.h;"                    >> $Cfile
 else
    echo "$Str2"                                                   >> $Cfile
 endif
 echo                                                              >> $Cfile

 set text = "stenex or se_noop"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModStenex};"                                       >> $Cfile
 if ( $?ParOpt ) then
    set text = "// parallel executable; stenex and pario included"
    echo $text                                                     >> $Cfile
    echo "Module ${ModPario};"                                     >> $Cfile
 else
    set text = "serial executable; noop stenex"
    echo $text                                                     >> $Cfile
 endif
 echo                                                              >> $Cfile

 set text = "par, par_nodistr and par_noop"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModPar};"                                          >> $Cfile
 echo                                                              >> $Cfile

 if ( $?build_twoway ) then
    echo "// option set for WRF-CMAQ twoway"                       >> $Cfile
    echo "Module ${ModTwoway};"                                    >> $Cfile
    echo                                                           >> $Cfile
 endif

 set text = "ctm_wrf and ctm_yamo"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModDriver};"                                       >> $Cfile
 echo                                                              >> $Cfile

 set text = "cartesian"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModGrid};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "init_yamo"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModInit};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "gencoor_wrf and gencoor"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModCpl};"                                          >> $Cfile
 echo                                                              >> $Cfile

 set text = "hyamo"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModHadv};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "vwrf and vyamo"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModVadv};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "multiscale"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModHdiff};"                                        >> $Cfile
 echo                                                              >> $Cfile

 set text = "acm2 and acm2_mp"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModVdiff};"                                        >> $Cfile
 echo                                                              >> $Cfile

 set text = "m3dry and m3dry_mp"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModDepv};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "emis"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModEmis};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "beis3"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModBiog};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "smoke"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModPlmrs};"                                        >> $Cfile
 echo                                                              >> $Cfile

 set text = "cgrid_spcs_nml and cgrid_spcs_icl"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModCgrds};"                                        >> $Cfile
 echo                                                              >> $Cfile

 set text = "inline and table"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModPhot};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "smvgear, ros3, ebi_cb05cl, ebi_cb05tucl, ebi_cb05tump, ebi_saprc99, ebi_saprc07tb, and ebi_saprc07tc"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModGas};"                                          >> $Cfile
 echo                                                              >> $Cfile

 set text = "gas chemistry reactions modules"
 echo "// " $text                                                  >> $Cfile
 echo "Module ${ModMech};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "aero5, aero6, and aero6_mp"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModAero};"                                         >> $Cfile
 echo                                                              >> $Cfile

 set text = "acm_ae6 and acm_ae6_mp"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModCloud};"                                        >> $Cfile
 echo                                                              >> $Cfile

 set text = "pa, which requires the"
 echo "// options are" $text "replacement of the three"            >> $Cfile
 set text = "// global include files with their pa_noop counterparts"
 echo $text                                                        >> $Cfile
 echo "Module ${ModPa};"                                           >> $Cfile
 echo                                                              >> $Cfile

 set text = "util"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModUtil};"                                         >> $Cfile
 echo                                                              >> $Cfile

 if ( $?ModMisc ) then
    echo "Module ${ModMisc};"                                      >> $Cfile
    echo                                                           >> $Cfile
 endif

#> make the Makefile or the model executable

 unalias mv rm
 if ( $?MakeFileOnly ) then
    if ( $?CopySrc ) then
       $Blder -makefo $Cfile
    else
       $Blder -makefo -git_local $Cfile   # $Cfile = ${CFG}.bld
    endif
 else   # also compile the model
    if ( $?CopySrc ) then
       $Blder $Cfile
    else
       $Blder -git_local $Cfile
    endif
 endif
 mv Makefile $Bld/Makefile.$compiler
 if ( -e $Bld/Makefile.$compiler && -e $Bld/Makefile ) rm $Bld/Makefile
 ln -s ./Makefile.$compiler Makefile

 if ( $status != 0 ) then
    echo "   *** failure in $Blder ***"
    exit 1
 endif
 if ( -e "$Base/${CFG}" ) then
    echo "   >>> previous ${CFG} exists, re-naming to ${CFG}.old <<<"
    mv $Base/${CFG} $Base/${CFG}.old
 endif
 mv ${CFG}.bld $Bld/${CFG}

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#

exit
