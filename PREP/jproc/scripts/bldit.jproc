#!/bin/csh -f

# ====================== JPROCv5.2 Build Script ===================== 
# Usage: bldit.jproc >&! bldit.jproc.log                              
# Requirements: I/O API & netCDF libraries; a Fortran compiler
#
# To report problems or request help with this script/program:        
#             http://www.cmascenter.org
# =================================================================== 

#> Source the config.cmaq file to set the build environment
 source ../../../config.cmaq
 set echo

#:#:#:#:#:#:#:#:#:#:#:# Begin User Input Section #:#:#:#:#:#:#:#:#:#:#:#

#> Source Code Repository 
 setenv REPOROOT $CMAQ_MODEL/jproc        #> location of the JPROC source code
 set GlobInc = $CMAQ_MODEL/CCTM/ICL       #> location of the global include files
 set Mechs   = $CMAQ_MODEL/CCTM/MECHS     #> location of the chemistry mechanism include files

#> Working directory and application IDs
 set Base = $cwd                       #> working directory for compiling the source code
 set APPL  = v52                       #> model configuration ID
 set MODEL = JPROC_${APPL}_$EXEC_ID    #> executable name
 set CFG   = cfg.$MODEL                #> configuration file name

#> Controls for managing the source code compilation
 set CopySrc                           #> copy the source files into a working irectory
#set MakeFileOnly                      #> uncomment to build a Makefile, but do not compile; comment out to compile the model (default if not set)

#======================================#>
#> JPROC Science Modules
#======================================#>
#> NOTE: For the modules with multiple options, a note is provided on where to look in the
#>   source code archive for a list of the possible settings; users may also refer to the CMAQ documentation

 set ModDriver = jproc_table
 set Mechanism = cb05e51_ae6_aq        #> chemical mechanism (see $CMAQ_MODEL/CCTM/MECHS)
 set Tracer    = trac0                 #> tracer configuration directory under $CMAQ_MODEL/CCTM/MECHS [ default: no tracer species ]
 set ModMisc = $Mechs/$Mechanism/RXNS_DATA_MODULE.F90

#======================================#>
#> Computing System Configuration:
#>    Most of these settings are done in config.cmaq
#======================================#>

 set FC = ${myFC}                      #> path of Fortan compiler; set in config.cmaq
 set FP = $FC                          #> path of Fortan preprocessor; set in config.cmaq
 set Blder = $CMAQ_LIB/bldmake            #> location of model builder executable

#> Libraries/include files
 set LIOAPI   = "${IOAPI_DIR}/lib ${ioapi_lib}"      #> I/O API library directory
 set IOAPIMOD = "${IOAPI_DIR}/include"               #> I/O API module directory
 set NETCDF   = "${NETCDF_DIR}/lib ${netcdf_lib}"    #> netCDF library directory

#> Set compiler flags
 set F_FLAGS    = "${myFFLAGS} -I ${IOAPIMOD} -I."
 set F90_FLAGS  = "${myFRFLAGS} -I ${IOAPIMOD} -I."
 set CPP_FLAGS  = ""
 set C_FLAGS    = "${myCFLAGS} -DFLDMN"
 set LINK_FLAGS = "${myLINK_FLAG}"

#:#:#:#:#:#:#:#:#:#:#:# End of User Input Section :#:#:#:#:#:#:#:#:#:#:#:#:#

#> Check for CMAQ_HOME and CMAQ_LIB settings:
 if ( ! -e $CMAQ_HOME || ! -e $CMAQ_LIB || ! -e $CMAQ_MODEL ) then
    echo "   CMAQ_HOME, CMAQ_LIB, or CMAQ_MODEL directory not found"
    exit 1
 endif
 echo "    Model repository base path: $CMAQ_HOME"
 echo "                  library path: $CMAQ_LIB"
 echo "              source code path: $CMAQ_MODEL"
 
 set BLD_OS = `uname -s`        ## Script set up for Linux only 
 if ($BLD_OS != 'Linux') then
    echo "   $BLD_OS -> wrong bldit script for host!"
    exit 1
 endif

#> The "BLD" directory for checking out and compiling source code
 set Bld = $Base/BLD_${APPL}
 if ( ! -e "$Bld" ) then
    mkdir $Bld
 else
    if ( ! -d "$Bld" ) then
       echo "   *** target exists, but not a directory ***"
       exit 1
    endif
 endif
 cd $Bld

 set LIB1 = "-L${LIOAPI}"
 set LIB2 = "-L${NETCDF}"
 set LIBS = "$LIB1 $LIB2 ${extra_lib}"

 source $Base/relinc.jproc
 if ( $status ) exit 1

 set ICL_CONST = $Bld

#> make the config file

 set Cfile = ${CFG}.bld
 set quote = '"'

 echo                                                               > $Cfile
 echo "model       $MODEL;"                                        >> $Cfile
 echo                                                              >> $Cfile
 echo "FPP         $FP;"                                           >> $Cfile
 echo                                                              >> $Cfile
 set text = "$quote$CPP_FLAGS$quote;"
 echo "cpp_flags   $text"                                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f_compiler  $FC;"                                           >> $Cfile
 echo                                                              >> $Cfile
 echo "f_flags     $quote$F_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "f90_flags   $quote$F90_FLAGS$quote;"                        >> $Cfile
 echo                                                              >> $Cfile
 echo "c_flags     $quote$C_FLAGS$quote;"                          >> $Cfile
 echo                                                              >> $Cfile
 echo "link_flags  $quote$LINK_FLAGS$quote;"                       >> $Cfile
 echo                                                              >> $Cfile
 echo "libraries   $quote$LIBS$quote;"                             >> $Cfile
 echo                                                              >> $Cfile

 set text="// mechanism:"
 echo "$text ${Mechanism}"                                         >> $Cfile
 echo "// model repository: ${CMAQ_MODEL}"                            >> $Cfile
 echo                                                              >> $Cfile
 echo "include SUBST_CONST      $ICL_CONST/CONST.EXT;"             >> $Cfile

 set text = "jproc_table"
 echo "// options are" $text                                       >> $Cfile
 echo "Module ${ModDriver};"                                       >> $Cfile
 echo                                                              >> $Cfile

 if ( $?ModMisc ) then
    echo "MISC ${ModMisc};"                                      >> $Cfile
    echo                                                           >> $Cfile
 endif

#> make the Makefile or the model executable

 unalias mv rm
 if ( $?MakeFileOnly ) then
    if ( $?CopySrc ) then
       $Blder -makefo $Cfile
    else
       $Blder -makefo -git_local $Cfile   # $Cfile = ${CFG}.bld
     # totalview -a $Blder -makefo $Cfile
    endif
 else   # also compile the model
    if ( $?CopySrc ) then
       $Blder $Cfile
    else
       $Blder -git_local $Cfile
    endif
 endif
 mv Makefile $Bld/Makefile.$compiler
 if ( -e $Bld/Makefile.$compiler && -e $Bld/Makefile ) rm $Bld/Makefile
 ln -s ./Makefile.$compiler Makefile

 if ( $status != 0 ) then
    echo "   *** failure in $Blder ***"
    exit 1
 endif
 if ( -e "$Base/${CFG}" ) then
    echo "   >>> previous ${CFG} exists, re-naming to ${CFG}.old <<<"
    mv $Base/${CFG} $Base/${CFG}.old
 endif
 mv ${CFG}.bld $Bld/${CFG}

#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#:#
 exit
